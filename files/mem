(struct mem/context
  mem-avail)

(fun (mem/cpy dst src size)
  (while (> size 0)
    (set (u8 dst) (u8 src))
    (set dst (+ dst 1))
    (set src (+ src 1))
    (set size (- size 1))))

(fun (mem/set dst val size)
  (while (> size 0)
    (set (u8 dst) val)
    (set dst (+ dst 1))
    (set size (- size 1))))

;; Dynamic memory allocation
;;
;; We keep a list of available memory regions, sorted by address.  The list is circular,
;; and there is one link that points back to lower addresses, all the other links point
;; to higher addresses.  There is one entry in the list that will never be allocated and
;; never be merged with its neighbors.  This way, the list is never empty, which lets us
;; avoid a couple of annoying boundary conditions.
;;
;; We get more memory via sys/page-get and never return any.
;;
;; This is straight from Knuth and "Programming in C".

(struct mem/avail-region
  ar-size
  ar-link)

(def mem/dbg 0)

(fun (mem/dbg-out str)
  (if (mem/dbg
       (dbg/out str))))

(fun (mem/dbg-int lab num)
  (if (mem/dbg
       (dbg/int lab num))))

(fun (mem/dbg-avail)
  (if (mem/dbg
       (dbg/out "AVAIL\n")
       (var p (mem/mem-avail (ctxt/mem context)))
       (var q p)
       (var i 0)
       (while (begin
                (dbg/int "  block" q)
                (dbg/int "    size" (mem/ar-size q))
                (set q (mem/ar-link q))
                (and (not (== p q))
                     (< i 200)))
         (set i (+ i 1)))
       (if ((>= i 2000)
            (sys/exit 44))))))

(fun (mem/free block)
  (mem/dbg-int "FREE" block)
  (var r (- block 8))
  (var size (u64 r))
  (mem/dbg-int "  size" size)
  (var avail (mem/mem-avail (ctxt/mem context)))
  (var p avail)
  (var q (mem/ar-link p))
  (while 1
    (mem/dbg-int "  on" p)
    (if ((or (and (> r p) (< r q))  ;; between p and q
             (and (> r p) (<= q p))  ;; after p, and p is last (or only)
             (and (< r q) (<= q p))) ;; before q, and q is first (or only)
         (mem/dbg-out "    fits\n")
         (if ((and (== (+ p (mem/ar-size p)) r)
                   (== (+ r size) q))
              (mem/dbg-out "    merge both\n")
              (set (mem/ar-size p) (+ (mem/ar-size p) size (mem/ar-size q)))
              (set (mem/ar-link p) (mem/ar-link q)))
             ((== (+ p (mem/ar-size p)) r)
              (mem/dbg-out "    merge left\n")
              (set (mem/ar-size p) (+ (mem/ar-size p) size)))
             ((== (+ r size) q)
              (mem/dbg-out "    merge right\n")
              (set (mem/ar-size r) (+ size (mem/ar-size q)))
              (set (mem/ar-link r) (mem/ar-link q))
              (set (mem/ar-link p) r))
             (else
              (mem/dbg-out "    new region\n")
              (set (mem/ar-size r) size)
              (set (mem/ar-link r) q)
              (set (mem/ar-link p) r)))
         (goto done)))
    (set p q)
    (set q (mem/ar-link p))
    (if ((== p avail)
         ;; XXX - unpossible (actually, double free gets us here)
         (dbg/out "NO PLACE FOUND FOR FREED REGION\n")
         (goto done))))
  (label done)
  (mem/dbg-avail))

(fun (mem/alloc size)
  (mem/dbg-int "ALLOC" size)
  (if ((== size 0)
       (set size 1)))
  (set size (bit-and (+ size 7 8) (bit-not 7)))
  (mem/dbg-int "  adjusted" size)
  (var avail (mem/mem-avail (ctxt/mem context)))
  (var p avail)
  (var q (mem/ar-link p))
  (var r 0)
  (while 1
    (mem/dbg-int "  on" q)
    (var k (- (mem/ar-size q) size))
    (mem/dbg-int "    left" k)
    (if ((> k 0)
         (if ((< k mem/avail-region)
              (set r q)
              (set size (mem/ar-size q))
              (set (mem/ar-link p) (mem/ar-link q))
              (if ((== q avail)
                   (set (mem/mem-avail (ctxt/mem context)) p))))
             (else
              (set r (+ q k))
              (set (mem/ar-size q) k)))
         (set (u64 r) size)
         (set r (+ r 8))
         (goto done)))
    (set p q)
    (set q (mem/ar-link p))
    (if ((== p avail)
         ;; XXX - get more mem
         (dbg/out "MORE MEMORY\n")
         (var page (sys/page-get))
         (set (u64 page) size)
         (set r (+ page 8))
         (set page (+ page size))
         (set (u64 page) (- sys/page-size size))
         (mem/free (+ page 8))
         (goto done))))
  (label done)
  (mem/dbg-int "  result" r)
  (mem/dbg-avail)
  r)

(fun (mem/test)
  (var p1 (mem/alloc 256))
  (var p2 (mem/alloc 128))
  (var p3 (mem/alloc 3))
  (mem/free p1)
  (mem/free p3)
  (mem/free p2)
  (mem/dbg-avail))

(fun (mem/init-alloc)
  (var page (sys/page-get))
  (var first (+ page 8))
  (var second (+ first mem/avail-region))
  (set (mem/ar-size first) 0)
  (set (mem/ar-link first) second)
  (set (mem/ar-size second) (- sys/page-size 8 mem/avail-region))
  (set (mem/ar-link second) first)
  (set (mem/mem-avail (ctxt/mem context)) second))

(fun (mem/init)
  (var boot-disk (ctxt/disk context))
  (var boot-books (ctxt/books context))
  (var page (sys/page-get))
  (mem/set page 0 sys/page-size)
  (set context page)
  (set (ctxt/disk context) boot-disk)
  (set (ctxt/books context) boot-books)
  (var mem-context (+ page ctxt/contexts))
  (set (ctxt/mem context) mem-context)
  (mem/init-alloc))
