(fun (shell/readline buf len)
  (con/out "$ ")
  (con/in buf len)
  (con/out buf)
  (con/out "\n"))

(fun (shell/streq a b)
  (while (and (u8 a) (== (u8 a) (u8 b)))
   (set a (+ a 1))
   (set b (+ b 1)))
  (== (u8 a) (u8 b)))

(fun (shell/skip-whitespace ptr)
  (while (or (== (u8 ptr) 32) (== (u8 ptr) 10))
   (set ptr (+ ptr 1)))
  ptr)

(fun (shell/skip-non-whitespace ptr)
  (while (and (u8 ptr) (!= (u8 ptr) 32) (!= (u8 ptr) 10))
   (set ptr (+ ptr 1)))
  ptr)

(fun (shell/parse-args argv max-argc line)
  (var argc 0)
  (set line (shell/skip-whitespace line))
  (while (and (u8 line) (< argc max-argc))
   (set (u64 argv argc) line)
   (set argc (+ argc 1))
   (set line (shell/skip-non-whitespace line))
   (if (u8 line)
       (begin
        (set (u8 line) 0)
        (set line (shell/skip-whitespace (+ line 1))))))
  argc)

(fun (shell/con-out-with-nl ptr)
  (con/out ptr)
  (con/out "\n"))

(fun (shell/main)
  (mem line 256)
  (mem file ed/textbuf)
  (mem argv (* 8 16))
  (set (ed/textbuf-buf file) (sys/page-get))
  (set (ed/textbuf-max file) (* 16 1024))
  (set (ed/textbuf-len file) 0)
  (while 1
   (shell/readline line 256)
   (var argc (shell/parse-args argv 16 line))
   (var arg1 (u64 argv))
   (if (shell/streq arg1 "quit")
       (sys/call 60 0)
    (if (shell/streq arg1 "edit")
        (ed/ed file 0 0 0 1024 768)
     (if (shell/streq arg1 "run")
         (if (var book (sys/compile "user"
                                    (ed/textbuf-buf file) (+ (ed/textbuf-buf file) (ed/textbuf-len file))
                                    con/out 0))
             (begin
              (if (var main (sys/book-search-fun book "main"))
                  (main)
               (con/out "no user/main function\n"))
              (sys/book-free book))
          (con/out "compilation failed\n"))
      (if (shell/streq arg1 "list")
          (fs/list shell/con-out-with-nl)
       (con/out "?\n")))))))
