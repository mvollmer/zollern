@=

(struct shell/instance
  (:base app/instance)
  cur
  cur-name
  max
  output
  output-ptr
  create-textbuf
  create-ed)

(fun (shell/capture-output str inst)
  (str/cpy (shell/output-ptr inst) str)
  (set (shell/output-ptr inst) (+ (shell/output-ptr inst) (str/len str))))

(fun (shell/start-create inst)
  (var name (ed/textbuf-new))
  (var ed (ed/new 0 name))
  (set (shell/create-textbuf inst) name)
  (set (shell/create-ed inst) ed))

(fun (shell/end-create inst)
  (var name (shell/create-textbuf inst))
  (var ed (shell/create-ed inst))
  (ed/free ed)
  (set (shell/create-ed inst) 0)
  (var str (ed/textbuf-maybe-as-string name))
  (if ((and str (u8 str))
       (mem wbuf fs/wbuf)
       (fs/create wbuf)
       (fs/commit wbuf str)
       (set (shell/cur inst) -1)
       (str/cpy (shell/cur-name inst) str)))
  (ed/textbuf-free name)
  (set (shell/create-textbuf inst) 0))

(struct shell/ed-app
  (:base app/instance)
  ed-name
  ed-text
  ed-ed)

(fun (shell/ed-init inst name rbuf)
  (set (shell/ed-name inst) (str/dup name))
  (set (shell/ed-text inst) (ed/textbuf-new))
  (set (shell/ed-ed inst) (ed/new (shell/ed-name inst) (shell/ed-text inst)))
  (ed/textbuf-read-file (shell/ed-text inst) rbuf)
  (ed/highlight (shell/ed-ed inst)))

(fun (shell/ed-write inst)
  (mem wbuf fs/wbuf)
  (fs/create wbuf)
  (ed/textbuf-write-file (shell/ed-text inst) wbuf)
  (fs/commit wbuf (shell/ed-name inst))
  (set (ed/dirty (shell/ed-ed inst)) 0)
  (app/paint-needed inst))

(fun (shell/ed-save inst)
  (if ((ed/dirty (shell/ed-ed inst))
       (shell/ed-write inst))))

(fun (shell/ed-save-all)
  (var app (app/-ac-apps (ctxt/app context)))
  (while app
    (if ((str/eq (app/app-mode app) "ed")
         (shell/ed-save app)))
    (set app (app/-app-link app))))

(fun (shell/ed-cleanup inst)
  (if ((ed/dirty (shell/ed-ed inst))
       (shell/ed-write inst)))
  (ed/free (shell/ed-ed inst))
  (ed/textbuf-free (shell/ed-text inst))
  (mem/free (shell/ed-name inst)))

(fun (shell/ed-paint inst)
  (ed/paint-with-border (shell/ed-ed inst) 0 0 (app/app-width inst) (app/app-height inst) (app/app-focus inst)))

(fun (shell/ed-input inst state input)
  (set (ed/title (shell/ed-ed inst)) (shell/ed-name inst))
  (app/paint-needed inst)
  (ed/input (shell/ed-ed inst) state input))

(app/functions shell/ed-funcs
  shell/ed-init
  shell/ed-cleanup
  shell/ed-paint
  shell/ed-input)

(fun (shell/launch-ed name)
  (mem rbuf fs/rbuf)
  (if ((app/switch name "ed")
       (begin))
      ((fs/open rbuf name)
       (app/launch (app/new shell/ed-app shell/ed-funcs name rbuf) name "ed"))))

(fun (shell/save name)
  (var inst (app/find name "ed"))
  (if (inst
       (shell/ed-save inst))))

(fun (shell/remove inst)
  (if ((u8 (shell/cur-name inst))
       (fs/remove (shell/cur-name inst))
       (set (u8 (shell/cur-name inst)) 0))))

(fun (shell/help inst)
  (set (shell/output-ptr inst) (shell/output inst))
  (shell/capture-output "Shell keys:\n  RET - switch to app\n    e - edit source\n    r - restart\n    X - delete\n\nGlobal keys:\nC-x b - switch to shell\nC-x r - restart\nC-x e - edit source\nC-x q - quit\n\n  M-< - previous app\n  M-> - next app" inst))

(fun (shell/init inst)
  (set (shell/cur inst) 0)
  (set (shell/cur-name inst) (mem/alloc 512))
  (set (u8 (shell/cur-name inst)) 0)
  (set (shell/output inst) (mem/alloc 2048))
  (set (u8 (shell/output inst)) 0)
  (set (shell/create-textbuf inst) 0)
  (set (shell/create-ed inst) 0))

(fun (shell/cleanup inst)
  (mem/free (shell/output inst))
  (mem/free (shell/cur-name inst)))

(fun (shell/compute-flags node buf)
  (var name (fs/node-name node))
  (var app (app/find name "main"))
  (var ed-app (app/find name "ed"))
  (var bk (book/find name))
  (set (u8 buf 0) (if ((and ed-app
                            (ed/dirty (shell/ed-ed ed-app)))
                       ch/*)
                      (else ch/spc)))
  (set (u8 buf 1) (if ((and bk (book/is-out-of-date bk))
                       ch/@)
                      (else ch/spc)))
  (set (u8 buf 2) (if ((and app bk
                            (!= (app/-ft-book (app/-app-functions app)) bk))
                       ch/#)
                      (else ch/spc)))
  (set (u8 buf 3) 0))

(fun (shell/draw-node inst index node)
  (mem flags 10)
  (shell/compute-flags node flags)
  (var x (gfx/get-x))
  (gfx/draw-string flags)
  (gfx/advance-to (+ x 25))
  (if ((>= (shell/cur inst) 0)
       (if ((and (== index (shell/cur inst)) (app/app-focus inst))
            (gfx/set-inverse 1)
            (str/cpy (shell/cur-name inst) node))))
      ((str/eq (shell/cur-name inst) node)
       (set (shell/cur inst) index)
       (gfx/set-inverse 1)))
  (gfx/advance-to (+ x 30))
  (gfx/draw-string (fs/node-name node))
  (gfx/advance-to (+ x 200))
  (gfx/set-inverse 0)
  (gfx/newline))

(fun (shell/insert-node node nodes)
  (var i 0)
  (var tmp)
  (while (and (u64 nodes i)
              (> (str/cmp (fs/node-name (u64 nodes i)) (fs/node-name node)) 0))
    (set i (+ i 1)))
  (while (u64 nodes i)
    (set tmp (u64 nodes i))
    (set (u64 nodes i) node)
    (set node tmp)
    (set i (+ i 1)))
  (set (u64 nodes i) node)
  (set (u64 nodes (+ i 1)) 0))

(fun (shell/paint inst)
  (var x 0)
  (var y 0)
  (var w (app/app-width inst))
  (var h (app/app-height inst))
  (mem nodes (* 128 u64))
  (set (u64 nodes 0) 0)
  (book/reset-out-of-date)
  (fs/list shell/insert-node nodes)
  (gfx/draw-init x y w h)
  (gfx/rect 0 0 2 (app/app-height inst) (gfx/color 0 0 0))
  (gfx/set-font fonts/sans-l)
  (gfx/set-x (+ x 20))
  (gfx/mark-left)
  (gfx/set-font fonts/sans)
  (gfx/newline)
  (var i 0)
  (while (u64 nodes i)
    (shell/draw-node inst i (u64 nodes i))
    (set i (+ i 1)))
  (set (shell/max inst) i)
  (if ((shell/create-ed inst)
       (ed/paint (shell/create-ed inst) (+ (gfx/get-left) 30) (gfx/get-y) 200 (gfx/get-line-height) (app/app-focus inst)))
      (else
       (var xx (gfx/get-x))
       (gfx/advance-to (+ xx 25))
       (if ((== (shell/cur inst) (shell/max inst))
            (str/cpy (shell/cur-name inst) "")
            (gfx/set-inverse 1)))
       (gfx/advance-to (+ xx 30))
       (gfx/draw-string "________________")
       (gfx/advance-to (+ xx 200))
       (gfx/set-inverse 0)))
  (gfx/newline)
  (gfx/set-font fonts/mono)
  (gfx/newline)
  (gfx/draw-string (shell/output inst)))

(struct shell/text-app
  (:base app/instance)
  ta-text)

(fun (shell/text-init inst output)
  (set (shell/ta-text inst) output))

(fun (shell/text-cleanup inst)
  (sys/page-put (shell/ta-text inst)))

(fun (shell/text-paint inst)
  (var x 4)
  (var y 4)
  (var w (app/app-width inst))
  (var h (app/app-height inst))
  (var str (shell/ta-text inst))
  (gfx/rect 0 0 w h (gfx/color 255 255 255))
  (while (and (u8 str)
              (< y (- h 16)))
    (if ((== (u8 str) ch/nl)
         (set x 4)
         (set y (+ y 16)))
        (else
         (set x (+ x (gfx/glyph fonts/mono x y (u8 str))))))
    (if ((> x (- w 14))
         (set x 4)
         (set y (+ y 16))))
    (set str (+ str 1))))

(fun (shell/text-input inst state input))

(app/functions shell/text-funcs
  shell/text-init
  shell/text-cleanup
  shell/text-paint
  shell/text-input)

(fun (shell/text-capture-output str buf)
  (str/cpy (+ buf (str/len buf)) str))

(fun (shell/launch-book name)
  (var output (sys/page-get))
  (set (u8 output) 0)
  (shell/text-capture-output "Launching " output)
  (shell/text-capture-output name output)
  (shell/text-capture-output ":\n" output)
  (var fail 1)
  (if ((var book (book/get name shell/text-capture-output output))
       (if ((var main (book/search-fun book "main"))
            (set fail 0)
            (main))
           (else
            (shell/text-capture-output "no main function\n" output)))
       (book/unref book)))
  (if (fail
       (app/launch (app/new shell/text-app shell/text-funcs output) "compile" "log"))
      (else
       (sys/page-put output))))

(fun (shell/switch-to-or-run inst)
  (if ((and (u8 (shell/cur-name inst))
            (not (app/switch (shell/cur-name inst) "main")))
       (shell/launch-book (shell/cur-name inst)))))

(fun (shell/input inst state input)
  (app/paint-needed inst)
  (var ed (shell/create-ed inst))
  (if ((== state 0)
       (if ((== input gfx/ev-key-return)
            (if (ed
                 (shell/end-create inst))
                ((u8 (shell/cur-name inst))
                 (shell/switch-to-or-run inst))
                (else
                 (shell/start-create inst))))
           (ed
            (ed/input ed state input))
           ((== input gfx/ev-key-up)
            (if ((> (shell/cur inst) 0)
                 (set (shell/cur inst) (- (shell/cur inst) 1)))))
           ((== input gfx/ev-key-down)
            (if ((< (shell/cur inst) (shell/max inst))
                 (set (shell/cur inst) (+ (shell/cur inst) 1)))))
           ((== input ch/e)
            (if ((shell/cur-name inst)
                 (shell/launch-ed (shell/cur-name inst))
                 (app/focus-shell 0))))
           ((== input ch/v)
            (if ((shell/cur-name inst)
                 (shell/ed-save-all)
                 (tx/launch (shell/cur-name inst))
                 (app/focus-shell 0))))
           ((== input ch/s)
            (if ((shell/cur-name inst)
                 (shell/save (shell/cur-name inst)))))
           ((== input ch/r)
            (if ((shell/cur-name inst)
                 (shell/ed-save-all)
                 (shell/launch-book (shell/cur-name inst))
                 (app/focus-shell 0))))
           ((== input ch/S)
            (shell/ed-save-all))
           ((== input ch/X)
            (shell/remove inst))
           ((== input ch/Q)
            (shell/ed-save-all)
            (app/quit inst))
           ((== input ch/h)
            (shell/help inst))))))

(fun (shell/global-input inst app state input)
  (var dup-name)
  (if ((== state 0)
       (if  ((== input ch/a)
             (app/focus-shell 0))
            ((== input ch/b)
             (app/focus-shell 1))
            ((== input ch/e)
             (app/focus-shell 0)
             (set dup-name (str/dup (app/app-name app)))
             (shell/launch-ed (app/app-name app))
             (mem/free dup-name))
            ((== input ch/v)
             (shell/ed-save-all)
             (app/paint-needed inst)
             (app/focus-shell 0)
             (set dup-name (str/dup (app/app-name app)))
             (tx/launch dup-name)
             (mem/free dup-name))
            ((== input ch/r)
             (shell/ed-save-all)
             (app/paint-needed inst)
             (app/focus-shell 0)
             (set dup-name (str/dup (app/app-name app)))
             (shell/launch-book dup-name)
             (mem/free dup-name))
            ((== input ch/q)
             (if ((not (or (and (str/eq (app/app-name app) "intro")
                                (str/eq (app/app-mode app) "tx"))
                           (== app inst)))
                  (app/quit app))))))
      ((== state gfx/ev-state-control)
       (if ((== input ch/x)
            (app/focus-shell (not (app/app-focus inst))))))))

(fun (shell/goto-link inst link)
  (if ((not (app/switch link "tx"))
       (tx/launch link))))

(app/shell-functions shell/funcs
  shell/init
  shell/cleanup
  shell/paint
  shell/input
  shell/global-input
  shell/goto-link)

(fun (shell/main)
  (tx/launch "intro")
  (app/launch-shell (app/new shell/instance shell/funcs)))
