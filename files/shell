@cat 2 System stuff
@cat 15 Shell

@** The shell

The shell is the root [book ui] element. It maintains the narrow
navigation column at the right, and also the main application
area.

@=

(struct shell/instance
  (:base app/shell-instance)
  mode
  ;; state for the "read" mode
  cur-view-name
  catalog catalog-max
  ;; state for the "write" mode
  files cur max
  ;; state for the "run" mode
  cur-run-name
  ;; ui elements
  read-btn
  write-btn
  run-btn
  find-tin
  quit-btn)

(fun (shell/refresh)
  (var root (app/root))
  (if ((var shell (app/r-shell root))
       ((app/-sft-global-input (ui/-elt-functions shell)) shell 0 gfx/ev-state-control ch/l))))
@

@* Writing
@=

(struct shell/ed-app
  (:base app/instance)
  ed-name
  ed-text
  ed-ed)

(fun (shell/ed-dirty inst)
  (shell/refresh))

(fun (shell/ed-init inst name rbuf)
  (app/instance-init inst)
  (set (shell/ed-name inst) (str/dup name))
  (set (shell/ed-text inst) (ed/textbuf-new))
  (set (shell/ed-ed inst) (ed/new (shell/ed-name inst) (shell/ed-text inst)))
  (set (ed/dirty-callback (shell/ed-ed inst)) shell/ed-dirty)
  (set (ed/dirty-callback-data (shell/ed-ed inst)) inst)
  (ed/textbuf-read-file (shell/ed-text inst) rbuf)
  (ed/highlight (shell/ed-ed inst)))

(fun (shell/ed-write inst)
  (mem wbuf fs/wbuf)
  (fs/create wbuf)
  (ed/textbuf-write-file (shell/ed-text inst) wbuf)
  (fs/commit wbuf (shell/ed-name inst))
  (set (ed/dirty (shell/ed-ed inst)) 0)
  (ui/paint-needed inst)
  (shell/refresh))

(fun (shell/ed-save inst)
  (if ((ed/dirty (shell/ed-ed inst))
       (shell/ed-write inst))))

(fun (shell/ed-save-all)
  (var al (app/r-apps (app/root)))
  (while al
    (if ((str/eq (app/app-mode (app/al-app al)) "ed")
         (shell/ed-save (app/al-app al))))
    (set al (app/al-next al))))

(fun (shell/ed-fini inst)
  (if ((ed/dirty (shell/ed-ed inst))
       (shell/ed-write inst)))
  (ed/free (shell/ed-ed inst))
  (ed/textbuf-free (shell/ed-text inst))
  (mem/free (shell/ed-name inst))
  (app/instance-fini inst))

(fun (shell/ed-paint inst)
  (ed/paint-with-border (shell/ed-ed inst) 0 0 (ui/elt-width inst) (ui/elt-height inst) (ui/elt-has-focus inst)))

(fun (shell/ed-input inst state input)
  (set (ed/title (shell/ed-ed inst)) (shell/ed-name inst))
  (ui/paint-needed inst)
  (ed/input (shell/ed-ed inst) state input))

(app/instance-functions shell/ed-funcs
  shell/ed-init
  shell/ed-fini
  shell/ed-paint
  shell/ed-input)

(fun (shell/launch-ed name)
  (mem rbuf fs/rbuf)
  (if ((app/switch name "ed")
       (begin))
      ((fs/open rbuf name)
       (app/launch (ui/new shell/ed-app shell/ed-funcs name rbuf) name "ed"))))

(fun (shell/save name)
  (var inst (app/find name "ed"))
  (if (inst
       (shell/ed-save inst))))

@* Running
@=
(struct shell/text-app
  (:base app/instance)
  ta-text)

(fun (shell/text-init inst output)
  (app/instance-init inst)
  (set (shell/ta-text inst) output))

(fun (shell/text-fini inst)
  (sys/page-put (shell/ta-text inst))
  (app/instance-fini inst))

(fun (shell/text-paint inst)
  (var x 4)
  (var y 4)
  (var w (ui/elt-width inst))
  (var h (ui/elt-height inst))
  (var str (shell/ta-text inst))
  (while (and (u8 str)
              (< y (- h 16)))
    (if ((== (u8 str) ch/nl)
         (set x 4)
         (set y (+ y 16)))
        (else
         (set x (+ x (gfx/glyph fonts/mono x y (u8 str))))))
    (if ((> x (- w 14))
         (set x 4)
         (set y (+ y 16))))
    (set str (+ str 1))))

(fun (shell/text-input inst state input))

(app/instance-functions shell/text-funcs
  shell/text-init
  shell/text-fini
  shell/text-paint
  shell/text-input)

(fun (shell/text-capture-output str buf)
  (str/cpy (+ buf (str/len buf)) str))

(fun (shell/launch-book name)
  (var output (sys/page-get))
  (set (u8 output) 0)
  (shell/text-capture-output "Launching " output)
  (shell/text-capture-output name output)
  (shell/text-capture-output ":\n" output)
  (var fail 1)
  (if ((var book (book/get name shell/text-capture-output output))
       (if ((var main (book/search-fun book "main"))
            (set fail 0)
            (main))
           (else
            (shell/text-capture-output "no main function\n" output)))
       (book/unref book)))
  (if (fail
       (app/launch (ui/new shell/text-app shell/text-funcs output) "compile" "log"))
      (else
       (sys/page-put output)))
  (shell/refresh))
@

@* ...
@=

(struct shell/file-data
  fd-name
  fd-flags)

(fun (shell/cur-name inst)
  (shell/fd-name (shell/file-data (shell/files inst) (shell/cur inst))))

(fun (shell/is-unsaved name)
  (var app (app/find name "ed"))
  (and app (ed/dirty (shell/ed-ed app))))

(fun (shell/compute-flags node)
  (var name (fs/node-name node))
  (var bk (book/find name))
  (or (shell/is-unsaved name)
      (and bk (book/is-out-of-date bk shell/is-unsaved 0))))

(fun (shell/free-file-data files)
  (var i 0)
  (while (shell/fd-name (shell/file-data files i))
    (mem/free (shell/fd-name (shell/file-data files i)))
    (set i (+ i 1)))
  (mem/free files))

(fun (shell/insert-file node files)
  (var i 0)
  (var tmp)
  (var name (str/dup (fs/node-name node)))
  (var flags (shell/compute-flags node))
  (while (and (shell/fd-name (shell/file-data files i))
              (> (str/cmp (shell/fd-name (shell/file-data files i)) name) 0))
    (set i (+ i 1)))
  (while (shell/fd-name (shell/file-data files i))
    (set tmp (shell/fd-name (shell/file-data files i)))
    (set (shell/fd-name (shell/file-data files i)) name)
    (set name tmp)
    (set tmp (shell/fd-flags (shell/file-data files i)))
    (set (shell/fd-flags (shell/file-data files i)) flags)
    (set flags tmp)
    (set i (+ i 1)))
  (set (shell/fd-name (shell/file-data files i)) name)
  (set (shell/fd-flags (shell/file-data files i)) flags)
  (set (shell/fd-name (shell/file-data files (+ i 1))) 0))

(fun (shell/update-files inst)
  (var files (mem/alloc (* 128 shell/file-data)))
  (set (shell/fd-name (shell/file-data files 0)) 0)
  (book/reset-out-of-date)
  (fs/list shell/insert-file files)
  (var i 0)
  (while (shell/fd-name (shell/file-data files i))
    ;; (dbg/str " name" (shell/fd-name (shell/file-data files i)))
    ;; (dbg/int " flags" (shell/fd-flags (shell/file-data files i)))
    (set i (+ i 1)))
  (set (shell/max inst) i)
  (if ((shell/files inst)
       (var cur-name (shell/cur-name inst))
       (set i 0)
       (set (shell/cur inst) 0)
       (while (shell/fd-name (shell/file-data files i))
         (if ((== (str/cmp cur-name (shell/fd-name (shell/file-data files i))) 0)
              (set (shell/cur inst) i)))
         (set i (+ i 1)))
       (shell/free-file-data (shell/files inst))
       (set (shell/files inst) files))
      (else
       (set (shell/cur inst) 0)))
  (set (shell/files inst) files)
  (ui/paint-needed inst))

(struct shell/catalog-data
  cd-title
  cd-level
  cd-sort
  cd-name)

(fun (shell/free-catalog-data catalog)
  (var i 0)
  (while (shell/cd-title (shell/catalog-data catalog i))
    (mem/free (shell/cd-title (shell/catalog-data catalog i)))
    (if ((shell/cd-name (shell/catalog-data catalog i))
         (mem/free (shell/cd-name (shell/catalog-data catalog i)))))
    (set i (+ i 1)))
  (mem/free catalog))

(struct shell/insert-data
  id-catalog
  id-level
  id-pos)

(fun (shell/dump-catalog catalog)
  (dbg/out "CATALOG\n")
  (var i 0)
  (while (shell/cd-title (shell/catalog-data catalog i))
    (dbg/str "title" (shell/cd-title (shell/catalog-data catalog i)))
    (dbg/int " level" (shell/cd-level (shell/catalog-data catalog i)))
    (dbg/int " name" (shell/cd-name (shell/catalog-data catalog i)))
    (set i (+ i 1))))

(fun (shell/insert-catalog-entry sort title id)
  (var catalog (shell/id-catalog id))
  (var level (shell/id-level id))
  (var pos (shell/id-pos id))
  ;(shell/dump-catalog catalog)
  ;; Find the entry that should come after ours.
  (while (begin
           (while (and (shell/cd-title (shell/catalog-data catalog pos))
                       (> (shell/cd-level (shell/catalog-data catalog pos)) level))
             (set pos (+ pos 1)))
           (and (shell/cd-title (shell/catalog-data catalog pos))
                (== (shell/cd-level (shell/catalog-data catalog pos)) level)
                (< (shell/cd-sort (shell/catalog-data catalog pos)) sort)))
    (set pos (+ pos 1)))
  ;; Only add our if it isn't already there
  (if ((not (and (shell/cd-title (shell/catalog-data catalog pos))
                 (== (shell/cd-level (shell/catalog-data catalog pos)) level)
                 (str/eq (shell/cd-title (shell/catalog-data catalog pos)) title)))
       (var i pos)
       (var tmp)
       (set title (str/dup title))
       (var name 0)
       (while (shell/cd-title (shell/catalog-data catalog i))
         (set tmp (shell/cd-title (shell/catalog-data catalog i)))
         (set (shell/cd-title (shell/catalog-data catalog i)) title)
         (set title tmp)
         (set tmp (shell/cd-level (shell/catalog-data catalog i)))
         (set (shell/cd-level (shell/catalog-data catalog i)) level)
         (set level tmp)
         (set tmp (shell/cd-sort (shell/catalog-data catalog i)))
         (set (shell/cd-sort (shell/catalog-data catalog i)) sort)
         (set sort tmp)
         (set tmp (shell/cd-name (shell/catalog-data catalog i)))
         (set (shell/cd-name (shell/catalog-data catalog i)) name)
         (set name tmp)
         (set i (+ i 1)))
       (set (shell/cd-title (shell/catalog-data catalog i)) title)
       (set (shell/cd-level (shell/catalog-data catalog i)) level)
       (set (shell/cd-sort (shell/catalog-data catalog i)) sort)
       (set (shell/cd-name (shell/catalog-data catalog i)) name)
       (set (shell/cd-title (shell/catalog-data catalog (+ i 1))) 0)))
  ;; Continue after us on next round, one level deeper
  (set pos (+ pos 1))
  (set (shell/id-level id) (+ (shell/id-level id) 1))
  (set (shell/id-pos id) pos))

(fun (shell/insert-book node catalog)
  (mem id shell/insert-data)
  (set (shell/id-catalog id) catalog)
  (set (shell/id-level id) 0)
  (set (shell/id-pos id) 0)
  (tx/list-catalog-entries (fs/node-name node) shell/insert-catalog-entry id)
  (var pos (- (shell/id-pos id) 1))
  (if ((not (shell/cd-name (shell/catalog-data catalog pos)))
       (set (shell/cd-name (shell/catalog-data catalog pos)) (str/dup (fs/node-name node))))))

(fun (shell/update-books inst)
  ;; urks, this is already half a page
  (var catalog (mem/alloc (* 256 shell/catalog-data)))
  (set (shell/cd-title (shell/catalog-data catalog 0)) 0)
  (fs/list shell/insert-book catalog)
  (var i 0)
  (while (shell/cd-title (shell/catalog-data catalog i))
    (set i (+ i 1)))
  (if ((shell/catalog inst)
       (shell/free-catalog-data (shell/catalog inst))))
  (set (shell/catalog inst) catalog)
  (set (shell/catalog-max inst) i))

(fun (shell/update inst)
  (shell/update-files inst)
  (shell/update-books inst))

@* Commands
@=

(fun (shell/cmd-view inst name)
  (if (name
       (ui/button-set-selected (shell/read-btn inst) 1)
       (ui/button-set-selected (shell/write-btn inst) 0)
       (ui/button-set-selected (shell/run-btn inst) 0)
       (if ((!= name (shell/cur-view-name inst))
            (if ((shell/cur-view-name inst)
                 (mem/free (shell/cur-view-name inst))))
            (set (shell/cur-view-name inst) (str/dup name))))
       (shell/ed-save-all)
       (tx/launch (shell/cur-view-name inst))
       (ui/paint-needed inst))))

(fun (shell/cmd-read inst)
  (shell/cmd-view inst (shell/cur-view-name inst)))

(fun (shell/cmd-edit inst name)
  (if (name
       (ui/button-set-selected (shell/read-btn inst) 0)
       (ui/button-set-selected (shell/write-btn inst) 1)
       (ui/button-set-selected (shell/run-btn inst) 0)
       (var i 0)
       (while (shell/fd-name (shell/file-data (shell/files inst) i))
         (if ((str/eq name (shell/fd-name (shell/file-data (shell/files inst) i)))
              (set (shell/cur inst) i)))
         (set i (+ i 1)))
       (shell/launch-ed name)
       (ui/paint-needed inst))))

(fun (shell/cmd-write inst)
  (shell/cmd-edit inst (shell/cur-name inst)))

(fun (shell/cmd-launch inst name)
  (if (name
       (ui/button-set-selected (shell/read-btn inst) 0)
       (ui/button-set-selected (shell/write-btn inst) 0)
       (ui/button-set-selected (shell/run-btn inst) 1)
       (if ((!= name (shell/cur-run-name inst))
            (if ((shell/cur-run-name inst)
                 (mem/free (shell/cur-run-name inst))))
            (set (shell/cur-run-name inst) (str/dup name))))
       (shell/ed-save-all)
       (shell/launch-book (shell/cur-run-name inst))
       (ui/paint-needed inst))))

(fun (shell/cmd-run inst)
  (shell/cmd-launch inst (shell/cur-run-name inst)))

(fun (shell/cmd-find inst str)
  (mem rbuf fs/rbuf)
  (if ((not (fs/open rbuf str))
       (mem wbuf fs/wbuf)
       (fs/create wbuf)
       (fs/commit wbuf str))
      (else
       (fs/close rbuf)))
  (shell/update inst)
  (shell/cmd-edit inst str)
  (ui/textinput-set (shell/find-tin inst) ""))

(fun (shell/cmd-remove inst)
  (if ((shell/cur-name inst)
       (fs/remove (shell/cur-name inst))
       (shell/update inst))))

(fun (shell/cmd-quit inst)
  (shell/ed-save-all)
  (sys/halt))

@* Instance
@=

(fun (shell/instance-init inst)
  (app/shell-instance-init inst)
  (set (shell/cur-view-name inst) 0)
  (set (shell/cur-run-name inst) 0)
  (set (shell/files inst) 0)
  (set (shell/catalog inst) 0)
  (shell/update inst)
  (set (shell/find-tin inst) (ui/textinput-new shell/cmd-find inst))
  (set (shell/read-btn inst) (ui/button-new "Read" shell/cmd-read inst))
  (set (shell/write-btn inst) (ui/button-new "Write" shell/cmd-write inst))
  (set (shell/run-btn inst) (ui/button-new "Run" shell/cmd-run inst))
  (set (shell/quit-btn inst) (ui/button-new "Quit" shell/cmd-quit inst))
  (ui/button-set-selected (shell/read-btn inst) 1))

(fun (shell/instance-fini inst)
  (mem/free (shell/cur-view-name inst))
  (mem/free (shell/cur-run-name inst))
  (shell/free-file-data (shell/files inst))
  (ui/free (shell/find-tin inst))
  (ui/free (shell/read-btn inst))
  (ui/free (shell/write-btn inst))
  (ui/free (shell/run-btn inst))
  (ui/free (shell/quit-btn inst))
  (app/instance-fini inst))

(fun (shell/paint-file inst index file cur-app)
  (var f fonts/sans)
  (var lh (- (gfx/font-ascent f) (gfx/font-descent f)))
  (var y (+ (* index (+ lh 2)) 40))
  (gfx/string 25 y fonts/sans (if ((shell/fd-flags file) "*") (else " ")))
  (gfx/string 40 y fonts/sans (shell/fd-name file))
  (if ((and cur-app (str/eq (app/app-name cur-app) (shell/fd-name file)))
       (ui/tint-selected 20 y (- (ui/elt-width inst) 40) lh))
      ((and (== index (shell/cur inst)) (ui/elt-has-focus inst))
       (gfx/invert-rect 20 y (- (ui/elt-width inst) 40) lh))))

(fun (shell/paint-catalog-entry inst index entry cur-app)
  (var f fonts/sans)
  (var lh (- (gfx/font-ascent f) (gfx/font-descent f)))
  (var y (+ (* index (+ lh 2)) 40))
  (gfx/string (+ 24 (* 10 (shell/cd-level entry))) y fonts/sans (shell/cd-title entry))
  (if ((and cur-app (shell/cd-name entry) (str/eq (app/app-name cur-app) (shell/cd-name entry)))
       (ui/tint-selected 20 y (- (ui/elt-width inst) 40) lh))))

(fun (shell/instance-paint inst)
  (var x 0)
  (var y 0)
  (var w (ui/elt-width inst))
  (var h (ui/elt-height inst))
  (gfx/rect 0 0 1 (ui/elt-height inst) (gfx/color 0 0 0))
  (var i 0)
  (var cur-app (app/r-current (app/root)))
  (if ((ui/btn-selected (shell/read-btn inst))
       (var catalog (shell/catalog inst))
       (while (shell/cd-title (shell/catalog-data catalog i))
         (shell/paint-catalog-entry inst i (shell/catalog-data catalog i) cur-app)
         (set i (+ i 1))))
      ((ui/btn-selected (shell/write-btn inst))
       (var files (shell/files inst))
       (while (shell/fd-name (shell/file-data files i))
         (shell/paint-file inst i (shell/file-data files i) cur-app)
         (set i (+ i 1)))))
  (var bw (/ (- (ui/elt-width inst) (* 4 ui/gap)) 3))
  (var bh ui/button-height)
  (ui/paint inst (shell/read-btn inst) ui/gap ui/gap bw bh)
  (ui/paint inst (shell/write-btn inst) (+ ui/gap bw ui/gap) ui/gap bw bh)
  (ui/paint inst (shell/run-btn inst) (+ ui/gap bw ui/gap bw ui/gap) ui/gap bw bh)
  (var ui-top (- (ui/elt-height inst) ui/textinput-height ui/gap bh ui/gap))
  (ui/paint inst (shell/find-tin inst) ui/gap ui-top (- (ui/elt-width inst) ui/gap ui/gap) ui/textinput-height)
  (ui/paint inst (shell/quit-btn inst) ui/gap (+ ui-top ui/textinput-height ui/gap) (- (ui/elt-width inst) ui/gap ui/gap) bh))

(fun (shell/switch-to-or-run inst)
  (if ((and (u8 (shell/cur-name inst))
            (not (app/switch (shell/cur-name inst) "main")))
       (shell/launch-book (shell/cur-name inst)))))

(fun (shell/instance-input inst state input x y)
  (var f fonts/sans)
  (var lh (+ (- (gfx/font-ascent f) (gfx/font-descent f)) 2))
  (if ((== input gfx/ev-button-1-press)
       (var l (/ (- y 40) lh))
       (if ((ui/btn-selected (shell/read-btn inst))
            (if ((and (>= l 0) (< l (shell/catalog-max inst)))
                 (shell/cmd-view inst (shell/cd-name (shell/catalog-data (shell/catalog inst) l))))))
           ((ui/btn-selected (shell/write-btn inst))
            (if ((and (>= l 0) (< l (shell/max inst)))
                 (set (shell/cur inst) l)
                 (shell/cmd-edit inst (shell/cur-name inst))
                 (ui/paint-needed inst))))))
      ((== state 0)
       (ui/paint-needed inst)
       (if ((== input gfx/ev-key-return)
            (if ((u8 (shell/cur-name inst))
                 (shell/switch-to-or-run inst))))
           ((== input gfx/ev-key-up)
            (if ((> (shell/cur inst) 0)
                 (set (shell/cur inst) (- (shell/cur inst) 1)))))
           ((== input gfx/ev-key-down)
            (if ((< (shell/cur inst) (- (shell/max inst) 1))
                 (set (shell/cur inst) (+ (shell/cur inst) 1)))))
           ((== input ch/s)
            (if ((shell/cur-name inst)
                 (shell/save (shell/cur-name inst)))))
           ((== input ch/e)
            (shell/cmd-edit inst (shell/cur-name inst)))
           ((== input ch/v)
            (shell/cmd-view inst (shell/cur-name inst)))
           ((== input ch/r)
            (shell/cmd-launch inst (shell/cur-name inst)))
           ((== input ch/S)
            (shell/ed-save-all))
           ((== input ch/X)
            (shell/cmd-remove inst))
           ((== input ch/Q)
            (shell/cmd-quit inst))))))

(fun (shell/instance-global-input inst app state input)
  (var dup-name)
  (if ((== state 0)
       (if  ((== input ch/e)
             (shell/cmd-edit inst (app/app-name app)))
            ((== input ch/v)
             (shell/cmd-view inst (app/app-name app)))
            ((== input ch/r)
             (shell/cmd-launch inst (app/app-name app)))
            ((== input ch/b)
             (ui/set-focus (shell/find-tin inst)))
            ((== input ch/q)
             (if ((not (or (and (str/eq (app/app-name app) "intro")
                                (str/eq (app/app-mode app) "tx"))
                           (== app inst)))
                  (app/quit app))))))
      ((== state gfx/ev-state-control)
       (if ((== input ch/l)
            (shell/update inst)
            (ui/paint-needed (app/r-shell (app/root))))
           ((== input ch/x)
            (app/focus-shell (not (ui/elt-contains-focus inst))))))))

(fun (shell/instance-goto-link inst link)
  (if ((not (app/switch link "tx"))
       (tx/launch link))))

(app/shell-functions shell/funcs
  shell/instance-init
  shell/instance-fini
  shell/instance-paint
  shell/instance-input
  shell/instance-global-input
  shell/instance-goto-link)

(fun (shell/main)
  (var inst (ui/new shell/instance shell/funcs))
  (app/launch-shell inst)
  (shell/cmd-view inst "intro"))
