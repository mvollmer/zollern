(struct shell/instance
  cur
  cur-name
  max
  output
  output-ptr
  create-textbuf
  create-ed)

(fun (shell/paint-string font x y str)
  (var ox x)
  (while (and (u8 str)
              (< x (- (gfx/width) 14))
              (< y (- (gfx/height) 16)))
    (if ((== (u8 str) ch/nl)
         (set x ox)
         (set y (+ y 16)))
        (else
         (set x (+ x (gfx/glyph font x y (u8 str))))))
    (set str (+ str 1))))

(fun (shell/paint-node inst x y index node)
  (set y (+ y 32 (* 16 index)))
  (shell/paint-string fonts/sans (+ x 24) y (fs/node-name node))
  (if ((>= (shell/cur inst) 0)
       (if ((== index (shell/cur inst))
            (gfx/invert-rect (+ x 20) y 200 16)
            (str/cpy (shell/cur-name inst) node))))
      ((str/eq (shell/cur-name inst) node)
       (set (shell/cur inst) index)
       (gfx/invert-rect (+ x 20) y 200 16))))

(fun (shell/insert-node node nodes)
  (var i 0)
  (var tmp)
  (while (and (u64 nodes i)
              (> (str/cmp (fs/node-name (u64 nodes i)) (fs/node-name node)) 0))
    (set i (+ i 1)))
  (while (u64 nodes i)
    (set tmp (u64 nodes i))
    (set (u64 nodes i) node)
    (set node tmp)
    (set i (+ i 1)))
  (set (u64 nodes i) node)
  (set (u64 nodes (+ i 1)) 0))

(fun (shell/paint inst x y w h)
  (mem nodes (* 128 u64))
  (set (u64 nodes 0) 0)
  (fs/list shell/insert-node nodes)
  (gfx/rect x y w h (gfx/color 255 255 255))
  (shell/paint-string fonts/sans-l (+ x 4) (+ y 4) "Welcome to Zollern")
  (var i 0)
  (while (u64 nodes i)
    (shell/paint-node inst x y i (u64 nodes i))
    (set i (+ i 1)))
  (set (shell/max inst) i)
  (set y (+ y 32 (* 16 i)))
  (if ((shell/create-ed inst)
       (ed/paint (shell/create-ed inst) (+ x 24) y 200 20))
      (else
       (shell/paint-string fonts/mono (+ x 24) y "________________")
       (if ((== (shell/cur inst) (shell/max inst))
            (str/cpy (shell/cur-name inst) "")
            (gfx/invert-rect (+ x 20) y 200 16)))))
  (shell/paint-string fonts/mono (+ x 24) (+ y 56) (shell/output inst)))

(fun (shell/capture-output str inst)
  (str/cpy (shell/output-ptr inst) str)
  (set (shell/output-ptr inst) (+ (shell/output-ptr inst) (str/len str))))

(fun (shell/run inst)
  (if ((not (gfx/switch (shell/cur-name inst)))
       (set (shell/output-ptr inst) (shell/output inst))
       (if ((and (u8 (shell/cur-name inst))
                 (var book (sys/book-get (shell/cur-name inst) shell/capture-output inst)))
            (if ((var main (sys/book-search-fun book "main"))
                 (main))
                (else
                 (shell/capture-output "no main function\n" inst)))
            (sys/book-unref book))))))

(fun (shell/start-create inst)
  (var name (ed/textbuf-new))
  (var ed (ed/new 0 name))
  (set (shell/create-textbuf inst) name)
  (set (shell/create-ed inst) ed))

(fun (shell/end-create inst)
  (var name (shell/create-textbuf inst))
  (var ed (shell/create-ed inst))
  (ed/free ed)
  (set (shell/create-ed inst) 0)
  (var str (ed/textbuf-maybe-as-string name))
  (if ((and str (u8 str))
       (mem wbuf fs/wbuf)
       (fs/create wbuf)
       (fs/commit wbuf str)
       (set (shell/cur inst) -1)
       (str/cpy (shell/cur-name inst) str)))
  (ed/textbuf-free name)
  (set (shell/create-textbuf inst) 0))

(struct shell/ed-data
  ed-name
  ed-text
  ed-ed
  ed-output)

(fun (shell/ed-write inst)
  (mem wbuf fs/wbuf)
  (fs/create wbuf)
  (ed/textbuf-write-file (shell/ed-text inst) wbuf)
  (fs/commit wbuf (shell/ed-name inst))
  (set (ed/dirty (shell/ed-ed inst)) 0))

(fun (shell/ed-capture str ptrloc)
  (dbg/str "CAP" str)
  (str/cpy (u64 ptrloc) str)
  (set (u64 ptrloc) (+ (u64 ptrloc) (str/len str))))
  
(fun (shell/ed-run inst)
  (var ptr (shell/ed-output inst))
  (set (u8 ptr) 0)
  (if ((var book (sys/book-get (shell/ed-name inst) shell/ed-capture (loc ptr)))
       (if ((var main (sys/book-search-fun book "main"))
            (main))
           (else
            (str/cpy ptr "no main")))
       (sys/book-unref book)))
  (if ((u8 (shell/ed-output inst))
       (set (ed/title (shell/ed-ed inst)) (shell/ed-output inst)))))

(fun (shell/ed-quit inst)
  (gfx/quit inst)
  (ed/free (shell/ed-ed inst))
  (ed/textbuf-free (shell/ed-text inst))
  (mem/free (shell/ed-name inst))
  (mem/free (shell/ed-output inst))
  (mem/free inst))

(fun (shell/ed-paint inst x y w h)
  (ed/paint-with-border (shell/ed-ed inst) x y w h))

(fun (shell/ed-input inst state input)
  (set (ed/title (shell/ed-ed inst)) (shell/ed-name inst))
  (if ((and (== state gfx/ev-state-c-x) (== input ch/s))
       (shell/ed-write inst))
      ((and (== state gfx/ev-state-c-x) (== input ch/q))
       (shell/ed-write inst)
       (shell/ed-quit inst))
      ((and (== state gfx/ev-state-c-x) (== input ch/r))
       (shell/ed-write inst)
       (shell/ed-run inst))
      (else
       (ed/input (shell/ed-ed inst) state input))))

(sys/funtab shell/ed-efuns
  shell/ed-paint
  shell/ed-input)

(fun (shell/ed-launch inst)
  (mem rbuf fs/rbuf)
  (var name (mem/alloc (+ (str/len (shell/cur-name inst)) 4)))
  (str/cpy name "ed:")
  (str/cpy (+ name 3) (shell/cur-name inst))
  (if ((gfx/switch name)
       (begin))
      ((fs/open rbuf (shell/cur-name inst))
       (var ed-inst (mem/alloc shell/ed-data))
       (set (shell/ed-name ed-inst) (str/dup (shell/cur-name inst)))
       (set (shell/ed-text ed-inst) (ed/textbuf-new))
       (set (shell/ed-ed ed-inst) (ed/new (shell/ed-name ed-inst) (shell/ed-text ed-inst)))
       (set (shell/ed-output ed-inst) (mem/alloc 500))
       (ed/textbuf-read-file (shell/ed-text ed-inst) rbuf)
       (ed/highlight (shell/ed-ed ed-inst))
       (gfx/launch ed-inst name shell/ed-efuns)))
  (mem/free name))

(fun (shell/remove inst)
  (if ((u8 (shell/cur-name inst))
       (fs/remove (shell/cur-name inst))
       (set (u8 (shell/cur-name inst)) 0))))

(fun (shell/help inst)
  (set (shell/output-ptr inst) (shell/output inst))
  (shell/capture-output "  RET - edit\n    r - run\n    X - delete\n\nC-x q - quit" inst))

(fun (shell/quit inst)
  (gfx/quit inst)
  (mem/free (shell/output inst))
  (mem/free (shell/cur-name inst))
  (mem/free inst))

(fun (shell/input inst state input)
  (var ed (shell/create-ed inst))
  (if ((== state 0)
       (if ((== input gfx/ev-key-return)
            (if (ed
                 (shell/end-create inst))
                ((u8 (shell/cur-name inst))
                 (shell/run inst))
                (else
                 (shell/start-create inst))))
           (ed
            (ed/input ed state input))
           ((== input gfx/ev-key-up)
            (if ((> (shell/cur inst) 0)
                 (set (shell/cur inst) (- (shell/cur inst) 1)))))
           ((== input gfx/ev-key-down)
            (if ((< (shell/cur inst) (shell/max inst))
                 (set (shell/cur inst) (+ (shell/cur inst) 1)))))
           ((== input ch/e)
            (shell/ed-launch inst))
           ((== input ch/X)
            (shell/remove inst))
           ((== input ch/h)
            (shell/help inst))))
      ((and (== state gfx/ev-state-c-x) (== input ch/q))
       (shell/quit inst))))

(sys/funtab shell/efuns
  shell/paint
  shell/input)

(fun (shell/main)
  (var inst (mem/alloc shell/instance))
  (set (shell/cur inst) 0)
  (set (shell/cur-name inst) (mem/alloc 512))
  (set (u8 (shell/cur-name inst)) 0)
  (set (shell/output inst) (mem/alloc 2048))
  (set (u8 (shell/output inst)) 0)
  (set (shell/create-textbuf inst) 0)
  (set (shell/create-ed inst) 0)
  (gfx/launch inst "shell" shell/efuns))
